apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'gradle-one-jar'

sourceCompatibility = 1.7
version = '0.0.4'
group = 'de.kandid'
def theName = 'twiline'

buildscript {
	repositories {mavenCentral()}
	dependencies {classpath 'com.github.rholder:gradle-one-jar:1.0.4'}
}

idea {
	module {
		outputDir = file('build/classes/main')
		testOutputDir = file('build/classes/test')
	}
}

eclipse {
	classpath {defaultOutputDir = file('build/eclipse/classes')}
	project {natures 'org.eclipse.jdt.groovy.core.groovyNature', 'org.springsource.ide.eclipse.gradle.core.nature'}
}

repositories {
	mavenLocal()
	mavenCentral()
}

sourceSets {
	main {java {srcDir 'src'} resources {srcDir 'src'}}
}

dependencies {
	compile group: 'de.kandid', name: 'kandidlib', version: '0.4.0'
}

jar {baseName = theName}

processResources {
	eachFile {fileCopyDetails ->
		if (fileCopyDetails.getName().endsWith(".html"))
			fileCopyDetails.filter {line -> line.replaceAll("_version_", version)}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	baseName = theName
	classifier = 'sources'
	from sourceSets.main.allSource
}

task dist(type: OneJar) {
	mainClass = 'de.kandid.apps.twiline.Twiline'
	oneJarBuildDir.mkdirs()
	new File(oneJarBuildDir, 'one-jar.properties').write "one-jar.silent=true\n"
}

artifacts {
	archives jar
	archives sourcesJar
}